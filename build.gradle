plugins {
    id 'com.gradle.plugin-publish' version '0.9.9'
    id 'java-gradle-plugin'
}

repositories {
    mavenCentral()
}

apply plugin: 'groovy'
apply plugin: 'java-gradle-plugin'

dependencies {
    compile gradleApi()
    compile localGroovy()

    testCompile gradleTestKit()
    testCompile 'junit:junit:4.12'
    testCompile('org.spockframework:spock-core:1.0-groovy-2.4') {
        exclude module: 'groovy-all'
    }
}


group = 'com.onesignal'
version = '0.8.0'
description 'OneSignal Gradle Plugin'
// Run to upload a new version to the Gradle Plugin Portal
// ./gradlew publishPlugins

pluginBundle {
    website = 'http://onesignal.com/'
    vcsUrl = 'https://github.com/OneSignal/OneSignal-Gradle-Plugin'
    plugins {
        plugin {
            id = 'com.onesignal.androidsdk.onesignal-gradle-plugin'
            displayName = 'OneSignal Gradle Plugin'
            description = project.description
            tags = ['OneSignal', 'Android', 'push', 'notifications', 'GCM', 'FCM']
        }
    }
}

gradlePlugin {
    pluginSourceSet project.sourceSets.main
    testSourceSets project.sourceSets.test
    plugins {
        plugin {
            id = 'com.onesignal.androidsdk.onesignal-gradle-plugin'
            implementationClass = 'com.onesignal.androidsdk.GradleProjectPlugin'
        }
    }
}

test {
    testLogging {
        showStandardStreams = true
        exceptionFormat = 'full'
    }
}


// Build for Local Testing
apply plugin: 'maven'

uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: uri('../repo'))
        }
    }
}
// ## Steps to use a local copy of this plugin to test
// 1. Run from root to create local package
//      ./gradlew uploadArchives
// 2. In <projectRoot>/build.gradle of your test project add the following to buildscript -> repositories {..}
//      maven { url uri('../../repo') }
// 3. Update '../../repo' from above to your correct relative path
// 4. Add to buildscript -> dependencies
//      classpath 'com.onesignal:onesignal-gradle-plugin:0.8.0'
// 5. To your app/build.gradle add
//      apply plugin: com.onesignal.androidsdk.GradleProjectPlugin